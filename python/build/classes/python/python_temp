/* Python 3 Grammar
 * 
 * The grammar is based on the file of following Github:
 *https://github.com/antlr/grammars-v4/blob/master/python3/Python3.g4
 * 
 * All comments that start with "///" are copy-pasted from
 * The Python Language Reference
 * 
 * https://docs.python.org/3/reference/index.html
 * 
 */

grammar python;


tokens { INDENT, DEDENT }

@lexer::members {
  // A queue where extra tokens are pushed on (see the NEWLINE lexer rule).
  private java.util.LinkedList<Token> tokens = new java.util.LinkedList<>();
  // The stack that keeps track of the indentation level.
  private java.util.Stack<Integer> indents = new java.util.Stack<>();
  // The amount of opened braces, brackets and parenthesis.
  private int opened = 0;
  // The most recently produced token.
  private Token lastToken = null;
  @Override
  public void emit(Token t) {
    super.setToken(t);
    tokens.offer(t);
  }

  @Override
  public Token nextToken() {
    // Check if the end-of-file is ahead and there are still some DEDENTS expected.
    if (_input.LA(1) == EOF && !this.indents.isEmpty()) {
      // Remove any trailing EOF tokens from our buffer.
      for (int i = tokens.size() - 1; i >= 0; i--) {
        if (tokens.get(i).getType() == EOF) {
          tokens.remove(i);
        }
      }

      // First emit an extra line break that serves as the end of the statement.
      this.emit(commonToken(pythonParser.NEWLINE, "\n"));

      // Now emit as much DEDENT tokens as needed.
      while (!indents.isEmpty()) {
        this.emit(createDedent());
        indents.pop();
      }

      // Put the EOF back on the token stream.
      this.emit(commonToken(pythonParser.EOF, "<EOF>"));
    }

    Token next = super.nextToken();

    if (next.getChannel() == Token.DEFAULT_CHANNEL) {
      // Keep track of the last token on the default channel.
      this.lastToken = next;
    }

    return tokens.isEmpty() ? next : tokens.poll();
  }

  private Token createDedent() {
    CommonToken dedent = commonToken(pythonParser.DEDENT, "");
    dedent.setLine(this.lastToken.getLine());
    return dedent;
  }

  private CommonToken commonToken(int type, String text) {
    int stop = this.getCharIndex() - 1;
    int start = text.isEmpty() ? stop : stop - text.length() + 1;
    return new CommonToken(this._tokenFactorySourcePair, type, DEFAULT_TOKEN_CHANNEL, start, stop);
  }

  // Calculates the indentation of the provided spaces, taking the
  // following rules into account:
  //
  // "Tabs are replaced (from left to right) by one to eight spaces
  //  such that the total number of characters up to and including
  //  the replacement is a multiple of eight [...]"
  //
  //  -- https://docs.python.org/3.1/reference/lexical_analysis.html#indentation
  static int getIndentationCount(String spaces) {
    int count = 0;
    for (char ch : spaces.toCharArray()) {
      switch (ch) {
        case '\t':
          count += 8 - (count % 8);
          break;
        default:
          // A normal space char.
          count++;
      }
    }

    return count;
  }

  boolean atStartOfInput() {
    return super.getCharPositionInLine() == 0 && super.getLine() == 1;
  }
}

/*
 * parser rules
 */

//INPUT
single_input
	: 
	NEWLINE 
	| simple_stmt 
	| compound_stmt NEWLINE
	;
			
simple_stmt
	:
	small_stmt (';' small_stmt)* (';')? NEWLINE
	;
	
small_stmt
	: 
	'return' (test (',' test)* (',')?)? 
	| testlist_star_expr (':' test ('=' test)?)
	;

testlist_star_expr
	: 
	(test|'*' expr) (',' (test|'*' expr))* (',')?
	;	

test
	: 
	('not' comparison | comparison) ('if' ('not' comparison | comparison) 'else' test)? 
	;

comparison
	:
	expr (('<' |'>'	|'=='|'>='|'<='|'!=') expr)*
	;

compound_stmt
	: 
	if_stmt 
	| while_stmt 
	| funcdef 
	| classdef 
	;
//if
if_stmt
	: 
	'if' test ':' suite ('elif' test ':' suite)* ('else' ':' suite)?
	;

suite
	: 
	simple_stmt | NEWLINE INDENT (simple_stmt | compound_stmt)+ DEDENT
	;
//while
while_stmt
	: 
	'while' test ':' suite ('else' ':' suite)?
	;
//funcdef
funcdef
	: 
	'def' NAME parameters ('->' test)? ':' suite
	; 
	
parameters
	: 
	'(' (typedargslist)? ')'
	;
	
typedargslist
	: 
	tfpdef ('=' test)? (',' tfpdef ('=' test)?)* (',' ('*' (tfpdef)? (',' tfpdef ('=' test)?)* )?)?
	| '*' (tfpdef)? (',' tfpdef ('=' test)?)* 
	;
  
tfpdef
	: 
	NAME (':' test)?
	;

//classdef
classdef
	: 
	'class' NAME ('(' (arglist)? ')')? ':' suite
	;
///////////////////////////////////////////////////////////////////////////////////////////
expr
	: 
	atom trailer* (('*'|'/'|'%'|'+'|'-') atom trailer*)*
	;

atom
	:
	'(' testlist_comp? ')' 
	| '[' testlist_comp? ']' 
    | '{' dictorsetmaker? '}' 
    | NAME 
    | NUMBER 
    | STRING+ 
    | 'True' 
    | 'False'
    ;
    
testlist_comp
	: 
	(test|'*' expr) (  (',' (test|'*' expr))* (',')? )
	;
	
dictorsetmaker
	:
	(test ':' test )((',' (test ':' test))* (',')?) 
	| (test | '*' expr)( (',' (test | '*' expr))* (',')?)
	;

trailer
	: 
	'(' (arglist)? ')' 
	| '[' subscript (',' subscript)* (',')? ']' 
	| '.' NAME
	;
	
arglist
	: 
	argument (',' argument)*  (',')?
	;

argument
	:  
	test  
	| test '=' test 
	| '*' test 
	;
	
subscript
	: 
	test 
	| (test)? ':' (test)? (':' (test)?)?
	;

/*
 * lexer rules
 */

DEF : 'def';
RETURN : 'return';
IF : 'if';
ELIF : 'elif';
ELSE : 'else';
WHILE : 'while';
NOT : 'not';
TRUE : 'True';
FALSE : 'False';
CLASS : 'class';

ADD : '+';
MINUS : '-';
STAR : '*';
DIV : '/';

LESS_THAN : '<';
GREATER_THAN : '>';
EQUALS : '==';
NOT_EQ_2 : '!=';

ASSIGN : '=';

COMMA : ',';
COLON : ':';
SEMI_COLON : ';';

OPEN_PAREN : '(' {opened++;};
CLOSE_PAREN : ')' {opened--;};
OPEN_BRACK : '[' {opened++;};
CLOSE_BRACK : ']' {opened--;};
OPEN_BRACE : '{' {opened++;};
CLOSE_BRACE : '}' {opened--;};


SKIP_
 : ( SPACES | COMMENT | LINE_JOINING ) -> skip
 ;
 
 NAME
 : ID_START ID_CONTINUE*
 ;
 
 NUMBER
 : NON_ZERO_DIGIT DIGIT*
 | '0'+
 ;
 
 fragment SPACES
 : [ \t]+
 ;

fragment COMMENT
 : '#' ~[\r\n\f]*
 ;

fragment LINE_JOINING
 : '\\' SPACES? ( '\r'? '\n' | '\r' | '\f')
 ;

fragment ID_START
 : '_'
 | [A-Z]
 | [a-z]
 ;

fragment ID_CONTINUE
 : ID_START 
 | [0-9]
;

fragment NON_ZERO_DIGIT
 : [1-9]
 ;
 
fragment DIGIT
 : [0-9]
 ;
////////////////////////////////////////////////////////////////////////

STRING
 :  
 SHORT_STRING 
 | LONG_STRING 
 ;
fragment SHORT_STRING
 : '\'' ( '\\' .| '\\' NEWLINE | ~[\\\r\n\f'] )* '\''
 | '"' ( '\\' .| '\\' NEWLINE | ~[\\\r\n\f"] )* '"'
 ;

fragment LONG_STRING
 : '\'\'\'' LONG_STRING_ITEM*? '\'\'\''
 | '"""' LONG_STRING_ITEM*? '"""'
 ;

fragment LONG_STRING_ITEM
 : ~'\\'
 | '\\' .
 | '\\' NEWLINE
 ;
 
NEWLINE
 : ( {atStartOfInput()}?   SPACES
   | ( '\r'? '\n' | '\r' | '\f' ) SPACES?
   )
   {
     String newLine = getText().replaceAll("[^\r\n\f]+", "");
     String spaces = getText().replaceAll("[\r\n\f]+", "");
     int next = _input.LA(1);
     if (opened > 0 || next == '\r' || next == '\n' || next == '\f' || next == '#') {
       // If we're inside a list or on a blank line, ignore all indents, 
       // dedents and line breaks.
       skip();
     }
     else {
       emit(commonToken(NEWLINE, newLine));
       int indent = getIndentationCount(spaces);
       int previous = indents.isEmpty() ? 0 : indents.peek();
       if (indent == previous) {
         // skip indents of the same size as the present indent-size
         skip();
       }
       else if (indent > previous) {
         indents.push(indent);
         emit(commonToken(pythonParser.INDENT, spaces));
       }
       else {
         // Possibly emit more than 1 DEDENT token.
         while(!indents.isEmpty() && indents.peek() > indent) {
           this.emit(createDedent());
           indents.pop();
         }
       }
     }
   }
 ;